struct StabilizerChain{T<:AbstractTransversal, P<:AbstractPermutation}
    transversals::Vector{T}
    gens::Vector{Vector{P}} # generators for stabilizers

    function StabilizerChain(
        transversals::AbstractVector{<:AbstractTransversal},
        gens::AbstractVector{AbstractVector{Permutation}},
        # for some reason I could only get the tests to work when I specified the
        # type here, so using 'Permutation' instead of '<:AbstractPermutation'
    )
        @assert length(transversals) == length(gens)
        T = eltype(transversals)
        P = eltype(eltype(gens))
        return new{T,P}(transversals, gens)
    end
end

StabilizerChain{T, P}() where {T,P} = StabilizerChain(Vector{T}(), Vector{AbstractVector{P}}())

depth(sc::StabilizerChain) = length(sc.transversals)
transversals(sc::StabilizerChain) = sc.transversals
transversal(sc::StabilizerChain, i::Integer) = sc.transversals[i]
gens(sc::StabilizerChain, i::Integer) = sc.gens[i]

basis(sc::StabilizerChain, i::Integer) = first(transversal(sc, i))
basis(sc::StabilizerChain) = [basis(sc, i) for i in 1:depth(sc)]

schreier_sims(S::AbstractVector{<:AbstractPermutation}) =
    schreier_sims(Transversal{Int64, eltype(S), typeof(^)}, S)

function schreier_sims(::Type{T}, S::AbstractVector{P}) where {T<:AbstractTransversal, P<:AbstractPermutation}
    sc = StabilizerChain{T, P}()

    for s in S
        push!(sc, s)
    end

    return sc
end

function sift(sc::StabilizerChain, g::AbstractPermutation; start_at_depth=1)
    d = start_at_depth
    r = g # the residual

    for i in start_at_depth:depth(sc)
        T = transversal(sc, i)
        γ = basis(sc, i)^r
        if γ ∉ T
            return d, r
        else
            r = r*inv(T[γ])
            d += 1
            if r == one(r)
                return d, r
            end
        end
    end
    
    return depth(sc)+1, r
end

function Base.push!(sc::StabilizerChain, g::AbstractPermutation; at_depth::Integer=1)
    isone(g) && return sc
    @assert 1 ≤ at_depth ≤ depth(sc)+1

    for i in 1:at_depth-1
        β = basis(sc, i)
        @assert action(transversal(sc,i))(β, g) == β
    end


    d, r = sift(sc, g, start_at_depth=at_depth)

    isone(r) && return sc # g is already in the group generated by sc

    if d == depth(sc) + 1
        extend_chain!(sc, r)
    else
        extend_gens!(sc, r, at_depth=at_depth)
    end
    return sc
end

function extend_chain!(sc::StabilizerChain{T}, g::AbstractPermutation) where T
    β = AbstractPermutations.firstmoved(g)
    S = [g]
    tr = T(β, S, ^)
    push!(sc.transversals, tr)
    push!(sc.gens, S)
    # a potentially new Schreier generator on the next layer:
    schr = g^length(tr)
    # clearly β^schr == β, so it is a generator for the next stabilizer
    if !isone(schr)
        # here we can directly extend_chain and shortcut all checks + call to sift
        extend_chain!(sc, schr)
        # otherwise we could do
        # push!(sc, schr, at_depth=depth(sc)+1)
    end
    return sc
end

function extend_gens!(sc::StabilizerChain{T}, g::AbstractPermutation; at_depth::Integer) where T
    push!(sc.gens[at_depth], g) # add new generators
    
    # recompute transversal + process all the new schreier generators
    β = basis(sc, at_depth)
    S = gens(sc, at_depth)
    tr = T(β, S, ^)

    sc.transversals[at_depth] = tr

    # a potentially new Schreier generator on the next layer:
    schr = g^length(tr)
    # clearly β^schr == β, so it is a generator for the next stabilizer
    if !isone(schr)
        # here we can directly extend_chain and shortcut all checks + call to sift
        push!(sc, schr, at_depth=at_depth+1)
        # otherwise we could do
        # push!(sc, schr, at_depth=depth(sc)+1)
    end
    return sc
end
